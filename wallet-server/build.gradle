plugins {
    id 'com.palantir.docker' version '0.20.0'
    id 'org.springframework.boot' version '2.1.6.RELEASE'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.sergio.wallet.server'
version = '1.0'

sourceSets {
    intTest {
        java.srcDir file('src/intTest/java')
        resources.srcDir file('src/intTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

configurations {
    intTestImplementation.extendsFrom testImplementation
    intTestRuntimeOnly.extendsFrom testRuntimeOnly
}

docker {
    name 'wallet-server-sergio'
    files bootJar.outputs
    copySpec.into("build/libs")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    implementation "net.devh:grpc-server-spring-boot-starter:${springBootGrpcVersion}"

    implementation project(':grpc')

    runtimeOnly 'mysql:mysql-connector-java'

    // Dependencies for Unit tests only.
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation "org.hamcrest:hamcrest-library:${hamcrestVersion}"

    // Dependencies for Integration tests only.
    // none specific for intTestImplementation yet
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    mustRunAfter test
}

check.dependsOn integrationTest